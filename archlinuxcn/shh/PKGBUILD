# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=shh
pkgver=2025.6.4
pkgrel=1
pkgdesc="Automatic systemd service hardening guided by strace profiling"
arch=("x86_64")
url="https://github.com/desbma/shh"
license=("GPL-3.0-or-later")
depends=("gcc-libs" "glibc" "strace")
makedepends=("cargo")
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
b2sums=('11fe5c6c92ee732282677507ff19cc0b99485f30f4fc3f300de11a329f82ca702be75654cc1dee7784fab767610e7b27bb6c86ad99650a4142a1e1495c263dfc')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n "s/host: //p")"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export RUSTFLAGS='-C link-args=-Wl,-z,shstk'
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --bins
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
