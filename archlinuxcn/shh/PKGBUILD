# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=shh
pkgver=2025.6.5
pkgrel=1
pkgdesc="Automatic systemd service hardening guided by strace profiling"
arch=("x86_64")
url="https://github.com/desbma/shh"
license=("GPL-3.0-or-later")
depends=("gcc-libs" "glibc" "strace")
makedepends=("cargo")
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
b2sums=('0580c83293796fa19e110bab54444383fe6a4fe5bf1d70e3dfacf5afd5112376375ac4df4f7da1aaee63c2c3e9f93a48b83611a78a27118769a5b952ce044799')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n "s/host: //p")"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export RUSTFLAGS='-C link-args=-Wl,-z,shstk'
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --bins
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
