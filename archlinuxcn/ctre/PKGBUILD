# Maintainer: Integral <integral@member.fsf.org>
# Contributor: tocic <tocic at protonmail dot ch>
# Contributor: Ruijie Yu (first.last@outlook.com)

pkgname=ctre
_srcname=compile-time-regular-expressions
pkgver=3.10.0
pkgrel=1
pkgdesc="Compile Time Regular Expression in C++"
url="https://compile-time.re"
arch=('any')
license=('Apache-2.0')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hanickadot/${_srcname}/archive/v${pkgver}.tar.gz")
sha256sums=('b17e6c9a6cc0cea65132f62a6c699cefed952721063569d6339eb3ca471045e6')

build() {
	cmake -B build -S "${_srcname}-${pkgver}" -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${_srcname}-${pkgver}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 docs/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
