# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=safetensors
pkgname=python-safetensors
pkgver=0.6.1
pkgrel=1
epoch=1
pkgdesc='Simple, safe way to store and distribute tensors'
arch=('x86_64')
url='https://github.com/huggingface/safetensors'
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-setuptools
  python-setuptools-rust
  python-wheel
)
optdepends=(
  python-numpy
  python-pytorch
  python-tensorflow
  python-huggingface-hub
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/safetensors/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('aadea63a865ee224e75da16d317de75c356581e4978cdabdbcd649bff4de313fca869d962d04bf9476aa81bfd5ffba2bd89e27dd817f83c1227ecbb5871e1d83')

build() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
