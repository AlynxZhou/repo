# Maintainer: Integral <integral@member.fsf.org>
# Contributor: enamulhasanabid <enamulhabid at gmail dot com>
# Contributor: Ã©clairevoyant
# Contributor: dangerdev <dangerdev at disroot dot org>
# Contributor: Danct12 <danct12 at disroot dot org>
# Contributor: Bart Ribbers <bribbers at disroot dot org>

pkgname=waydroid-image
pkgver=20.0_20250809
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="LineageOS-based Android images for Waydroid"
arch=('x86_64')
url="https://waydro.id"
license=('Apache-2.0')
depends=('waydroid')
replaces=('waydroid-image-beta')
_srcprefix="https://sourceforge.net/projects/waydroid/files/images"
source=("${_srcprefix}/system/lineage/waydroid_x86_64/lineage-${_pkgver}-VANILLA-waydroid_x86_64-system.zip"
	"${_srcprefix}/vendor/waydroid_x86_64/lineage-${_pkgver}-MAINLINE-waydroid_x86_64-vendor.zip")
sha256sums=('c451aba59385dd03ba8443cf24c583ff409552984a7b3ec43891fba46c6503f1'
            '14df3ffc773a30c2c157fa6f97e6c79338990b60ac85d69559bbfb82772650ef')

package() {
	install -Dm644 {system,vendor}.img -t "${pkgdir}/usr/share/waydroid-extra/images/"
}
