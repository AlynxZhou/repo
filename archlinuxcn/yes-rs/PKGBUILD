# Maintainer: Roald Clark <roaldclark@gmail.com>

_name=yes-rs
pkgname="${_name}"
pkgver=0.2.0
pkgrel=1
pkgdesc="A blazingly fast, memory-safe rewrite of the classic Unix 'yes' command"
arch=(
    'aarch64'
    'x86_64'
)
url="https://github.com/jedisct1/${_name}"
license=(
    'Apache-2.0'
    'MIT'
)
depends=(
    'gcc-libs'
    'glibc'
)
makedepends=(
    'cargo'
)
source=("${_name}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
b2sums=('1c4287a86a711ea3bc30c07e5fca3600f59160d0ef7e9233b47ee5b7dbfb73736455563094dc885b3cce79fd80c2731287e7f8fecbc591d77218d21610566077')

prepare() {
    cd "${srcdir}/${_name}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    [[ ! -f "Cargo.lock" ]] && cargo update
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release --frozen --all-features
}

package() {
    cd "${srcdir}/${_name}-${pkgver}"
    install -Dm0755 "target/release/${_name}" -t "${pkgdir}/usr/bin/"
}
