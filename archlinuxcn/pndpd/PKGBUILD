# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=pndpd
pkgver=1.2.14
pkgrel=2
pkgdesc="NDP Proxy / Responder daemon (IPv6)"
arch=("x86_64" "i686")
url="https://github.com/Kioubit/pndpd"
license=('GPL-3.0-or-later')
provides=('pndpd')
conflicts=('pndpd')
depends=('glibc')
makedepends=('go')
backup=("etc/pndpd.conf")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('62b324bafe0fe4bea07394b513efec1098199bd0a68b3b207514ca6972170c49b2f4356a946b5775862cbc75c55b6cb67745e7b93a67bb43a4941d7b1069411e')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 pndpd -t "$pkgdir/usr/bin/"
  install -Dm644 pndpd.conf -t "$pkgdir/etc/"
  sed -i "s|/usr/local|/usr|" pndpd.service
  install -Dm644 pndpd.service -t "$pkgdir/usr/lib/systemd/system/"
}
