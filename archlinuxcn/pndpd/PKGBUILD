# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=pndpd
pkgver=1.2.14
pkgrel=1
pkgdesc="NDP Proxy / Responder daemon (IPv6)"
arch=("x86_64" "i686")
url="https://github.com/Kioubit/pndpd"
license=('GPL-3.0-or-later')
provides=('pndpd')
conflicts=('pndpd')
depends=('glibc')
makedepends=('go')
backup=("etc/pndpd.conf")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('1687c661644435d5c47412bc03e6703b16ada19715d53984ee4186cb471f6e5a808073bedd13d52843ad7f564ea77fad576b8323e05a471582e559cf0a81b724')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 pndpd -t "$pkgdir/usr/bin/"
  install -Dm644 pndpd.conf -t "$pkgdir/etc/"
  sed -i "s|/usr/local|/usr|" pndpd.service
  install -Dm644 pndpd.service -t "$pkgdir/usr/lib/systemd/system/"
}
