# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>
# Contributor: Bart Ribbers <bribbers@disroot.org>

# This PKGBUILD is base on the AUR repo waydroid, by Danct12 <danct12@disroot.org> and Bart Ribbers <bribbers@disroot.org>

pkgname='waydroid'
pkgver=1.5.3
pkgrel=1
pkgdesc='A container-based approach to boot a full Android system on a regular Linux system'
arch=('any')
url='https://github.com/waydroid/waydroid'
license=('GPL-3.0-or-later')
depends=('dbus-python' 'dnsmasq' 'lxc' 'nftables' 'python-gbinder' 'python-gobject' 'which')
optdepends=('waydroid-image: Android images for use with waydroid'
            'waydroid-image-gapps: Android images for use with waydroid, including gapps'
            'python-pyclip: Share clipboard with container')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('58d5a9384aa9755368a79992f69a863805b231f1f2f85c35eb46cc4a07b283b666a8d75cdc7651d28eee97001b81a4dc5353e7c3f32e301b19eab532083b9a29')

prepare() {
  sed -i '1s/env //' "${srcdir}/${pkgname}-${pkgver}/waydroid.py"
}

package() {
  DESTDIR="${pkgdir}" USE_NFTABLES=1 make -C "${srcdir}/${pkgname}-${pkgver}" install
}
