# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>
# Contributor: Bart Ribbers <bribbers@disroot.org>

# This PKGBUILD is base on the AUR repo waydroid, by Danct12 <danct12@disroot.org> and Bart Ribbers <bribbers@disroot.org>

pkgname='waydroid'
pkgver=1.5.2
pkgrel=1
pkgdesc='A container-based approach to boot a full Android system on a regular Linux system'
arch=('any')
url='https://github.com/waydroid/waydroid'
license=('GPL-3.0-or-later')
depends=('dbus-python' 'dnsmasq' 'lxc' 'nftables' 'python-gbinder' 'python-gobject' 'which')
optdepends=('waydroid-image: Android images for use with waydroid'
            'waydroid-image-gapps: Android images for use with waydroid, including gapps'
            'python-pyclip: Share clipboard with container')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('80be97409d73c3dc649482ff9b8a9b3c316f7cfa7560c87e26a2b68a0efdcaa34c070825334789b6830e3ee0a88eb0967e9101575e4eadc4a39bda099c0f7651')

prepare() {
  sed -i '1s/env //' "${srcdir}/${pkgname}-${pkgver}/waydroid.py"
}

package() {
  DESTDIR="${pkgdir}" USE_NFTABLES=1 make -C "${srcdir}/${pkgname}-${pkgver}" install
}
