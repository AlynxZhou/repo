pkgname=julia-git-tzjdata-src
pkgver=1.5.0+2025b
_commit=d385bb73fc6afb395d521a2d5cb101890b66e8fc
pkgrel=1
pkgdesc="TZJData.jl"
url="https://github.com/JuliaTime/TZJData.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts wget)
depends=(julia-git)
source=("git+https://github.com/JuliaTime/TZJData.jl.git#commit=$_commit")
sha256sums=('80b2f71636a2bcf6744aaa2222832f9fec46517292c0ac8515fe9cc137910865')

build() {
  cd TZJData.jl

  rm -rf gen

  local url
  url=$(sed -ne '/url *= *".*"/{s/ *url *= *"\(.*\)" */\1/p;q}' Artifacts.toml)

  wget -O tzjfile-tzdata.tar.gz "$url"

  (
    rm -rf tzjdata/
    mkdir tzjdata/
    cd tzjdata/
    tar xf ../tzjfile-tzdata.tar.gz
    rm ../tzjfile-tzdata.tar.gz
  )

  sed -i -e 's|artifact"tzjdata"|joinpath(@__DIR__, "../tzjdata")|' src/TZJData.jl
}

package() {
  cd TZJData.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh TZJData "${pkgdir}" "${pkgname}" julia-git
}
