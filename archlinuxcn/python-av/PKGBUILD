# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=15.0.0
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  cython
  python-build
  python-installer
  python-pip
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg
  glibc
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('47ba6cca7e2aa1258883350a7f55a439ceb56ebffb4d84d64626742eb9729c28791f1374a242dcac93d4b819a287b37c512dc95501d9f41bd1d260ca03eea9b5')

build() {
  cd "${_pkgname}-${pkgver}"
  GITHUB_ACTIONS=true \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
