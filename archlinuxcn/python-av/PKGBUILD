# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=15.0.0rc1
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  cython
  python-build
  python-installer
  python-pip
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg
  glibc
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('d39d5d6cd280217803774ef1d8c6a8a41cbd980f35074a0179d603a9185d454ec3971d66b1e22103a2d08ea70cc0c4e848bc637a14a23940bfbf05a885b53ce2')

build() {
  cd "${_pkgname}-${pkgver}"
  GITHUB_ACTIONS=true \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
