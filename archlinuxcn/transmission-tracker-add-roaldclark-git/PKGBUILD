# Maintainer: Roald Clark <roaldclark@gmail.com>

_pkgname=transmission-tracker-add
pkgname="${_pkgname}-roaldclark-git"
pkgver=20250427.174916
pkgrel=1
pkgdesc="A script tool for adding trackers to Transmission using pre-configured tracker lists"
arch=('any')
url="https://github.com/roaldclark/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('bash')
makedepends=('git')
optdepends=(
    'trackerslist-git: An updated list of public BitTorrent trackers'
    'trackerslist-collection-git: A regularly updated collection of popular BitTorrent trackers'
)
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
backup=("etc/default/${_pkgname}")
install="${_pkgname}.install"
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

package() {
    depends+=('transmission-cli')

    cd "${srcdir}/${_pkgname}"
    DESTDIR="${pkgdir}" PREFIX=/usr OWNER=root make install
}
