# Maintainer: Roald Clark <roaldclark at gmail dot com>
# Contributor: aulonsal <seraur at aulonsal dot com>

pkgname=f2
pkgver=2.1.1
pkgrel=1
pkgdesc="A cross-platform command-line tool for batch renaming files and directories quickly and safely"
arch=('x86_64' 'aarch64')
url="https://github.com/ayoisaiah/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=(
    'go'
    'gotestfmt'
    'just'
    'perl-image-exiftool'
)
conflicts=("${pkgname}-bin")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('a37f5a8743ba789ab603ba4f4868215067da3aa2156d5013b68a52cd26606d72cd7fbb81c31976d929ebed50faa32675ad9f045e03578ea64d845f399927f318')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    just build
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    just test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -Dm0644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm0644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
    install -Dm0755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
}
