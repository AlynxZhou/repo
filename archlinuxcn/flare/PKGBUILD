# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Julian <juliannfairfax@protonmail.com>

pkgname=flare
pkgver=0.16.3
_pkgver=${pkgver/beta/-beta/}
pkgrel=2
epoch=1
pkgdesc="Chat with your friends on Signal"
url="https://gitlab.com/schmiddi-on-mobile/${pkgname}"
arch=("i686" "x86_64" "armv6h" "armv7h" "aarch64")
license=("AGPL-3.0-or-later")
makedepends=("blueprint-compiler" "cargo" "meson")
depends=("libadwaita" "libsecret" "protobuf" "gtksourceview5" "libspelling")
optdepends=("feedbackd: for vibrating notifications")
source=("${url}/-/archive/${_pkgver}/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('bdbb12e032524b4daa9b138d94666850314be35b22ab85b7cd367c11a1516078')

build() {
	arch-meson "${pkgname}-${_pkgver}" build
	CFLAGS+=" -ffat-lto-objects" meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --no-rebuild --destdir "${pkgdir}"
	install -Dm644 ${pkgname}-${_pkgver}/doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
