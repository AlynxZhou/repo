# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=xsdata
pkgname=python-${_name}
pkgver=25.7
pkgrel=1
pkgdesc="Naive XML & JSON Bindings for python"
arch=(any)
url="https://github.com/tefra/${_name}"
license=(MIT)
depends=(python python-typing_extensions python-click python-click-default-group python-docformatter python-jinja python-toposort ruff python-requests python-lxml)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-pytest-benchmark)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('2932f6a5f7297a3151b0ff90a6a643eb67ce0ae94df0da38384e85c426575759b3b084cb2cce0e7fc0e8b1387493aa352e0191089bc4828e41f9df9aeaf4025d')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  cp -r docs "$pkgdir/usr/share/doc/$pkgname"
}
