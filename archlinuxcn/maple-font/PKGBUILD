# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgbase="maple-font"

pkgver=7.5
pkgrel=1
pkgdesc="Open source monospace font with round corner, ligatures and Nerd-Font icons for IDE and terminal. 带连字和控制台图标的圆角等宽字体，中英文宽度完美2:1。"
url="https://github.com/subframe7536/maple-font"
arch=("any")
license=("OFL-1.1")
makedepends=("unzip")

pkgname=()
source=()
noextract=()
sha256sums=('49fc3a94aa07f3c0441cb3115be876880c9d69ea6a8a5b72f41f97be9a3b598e'
            '02ef49fed39a67fd6306ef9828ac915b3aa14b94a4cefb5bbabf4702df93f537'
            '3bd076e9e0a0cd48397fe836abe4c299c34245a0c4ee043c5653159626f3e085'
            '206e1064d5611ffb18f8950088b54ffef4e8d53858c28205bd7d5c02b6180ba3'
            '2d496c8a5ac02daf3477d9a10e193c694489e9fa8d26193628b51086d5ee07a4'
            '51da1afc4ae7d2803bf38849c720e9a11c2c9f4accdcdef9eb9224bdb098628f'
            'd25858996a461d5b33f17a78313b91bba293c63a3746ebb4323cec69144f6380'
            '495b9a8691f4a041dd378a318b6d57f819ac86751d3f599c6937a38bce6fdcbc'
            '2a9b0c9b1f76708f690fcc9a014c93b6c945b6af02a59ee21125b7799b742f15'
            'da9e9b013fceff6321f2ecf53a27635e196c66eb12a5e3b0e461ae78f0266e90'
            '32327589f35a7ec9586ee38d27c3642f65ffcd9968598b1b74275ea7a709bae6'
            'b893777fa3539eae82171c9d3b35112195fecf734f7cbde969c0df3f011da449'
            '7af34b409972d034e11d3d38a7d7fee732ffe1d3bae1ea13c6507e0833ca56b5'
            '63bc152d4a6caf95640032577dd67e7fcf0c29036c107f85ecea3750abdbe8ea'
            '08ba3ebbd5d13eba488c425f777884e0ccb7c98bce00a1cadd2176370cf50309'
            'c63a1c62da9d94c0d75e70b1795e1f5f7b5df1e015e7be6151e3eac57dcbf4b1'
            '61ac53ace3720f5142b8ba277602cab4fd4b4b848a18f89a879295a4dd33c599'
            '755c5f8d55ee99ec735adf571fd5cce5649a46653e2fcd707e219b4dff24fcee'
            '07bfc8276d0ba2b5bbb81e60bce5e10c8b5a40e4bc63d7923b97c80e56acccd5'
            '54538c191986d066ddab13283bac54a173d78b4f265e241ae651595c00043cac'
            '6ee1638126b1d4064dc1db31abbcc2e25297c3fb5b79b9ea213c7f234846f9a8'
            '000e6b476a8677665728a35a79e43869454cfbdc576e263f1058bd620a81317d'
            'd3eee9ae7b04fb70c8f035435a580605320aa533e876bda67916b66f6c736a7e'
            '947b79da06fcdaf6c14fcee346799f8ba99a618e220961c42ebb2191f30c8005'
            '72591bd191ae67c3a1d6991d7bcba6bf4ea42d9fedf4e95afd51b44abb7dd1f2'
            'd2a358e43c24450e40a17a075770fcbc73a678db93b6cfa5a3f9ea047b1e2a7c'
            'd20627cb628a60fce4618ef19e9520e81abff7a34ec086b4ec46dc7339a7fdaa'
            '9b7d07b5645a82ae4cea1797a1d2530ab4fdfb82b3fa6914bfa33ac70f31494d'
            '17c7baf8ec9d73797e07030b513288fd60019e143669a30cb0cf0c174ee412da'
            'e49d0bcbe7152f6f48ec2946381def451ae0d82662e0034aa8cb683b14af9e17'
            '594be01a45bd0b78d280e49fb22f13f053c3a446bd1614b38a211d2e4dd60e0a'
            '93811b2062d4968f3a75f351aba56cf952a7ed8cb168f8cfa678f3e67646c36f'
            '034326fde392ab4a90001694fbf1eeef7656c2c96f7822fb833e3e54cfeffac5'
            'bc88f5f9428828095e6c05bc4e98fbf87ab448f88283c7716b63528b0e014405'
            'cd23ca6864d996b0a07cc9eb6817fc206e103209a67f93165c870b26cc5472ad'
            'e89ac787b6521a1ba39dfa0afa535040af22e4cb2a5c2728e96a7e12531a4a0c'
            'b20e99f40903552e1188a596d545d3a3039c361ea10cc751309e0341bfdc8804'
            'c41ef7fcc1e2284c67f861c87b38e4da6b1d93dc7e34850cf6f26b2678636056'
            '9e92d61370697ba022deb824c783cd46ce6cd83eea0d338a56f5e2f3db326034'
            '3315e63bc750130bc0daa499dd483922adff6a4857a874d20bc30ff6e5efa895')
_variant=("MapleMono-CN-unhinted"
          "MapleMono-CN"
          "MapleMono-NF-CN-unhinted"
          "MapleMono-NF-CN"
          "MapleMono-NF-unhinted"
          "MapleMono-NF"
          "MapleMono-OTF"
          "MapleMono-TTF"
          "MapleMono-Variable"
          "MapleMono-Woff2"
          "MapleMonoNL-CN-unhinted"
          "MapleMonoNL-CN"
          "MapleMonoNL-NF-CN-unhinted"
          "MapleMonoNL-NF-CN"
          "MapleMonoNL-NF-unhinted"
          "MapleMonoNL-NF"
          "MapleMonoNL-OTF"
          "MapleMonoNL-TTF"
          "MapleMonoNL-Variable"
          "MapleMonoNL-Woff2"
          "MapleMonoNormal-CN-unhinted"
          "MapleMonoNormal-CN"
          "MapleMonoNormal-NF-CN-unhinted"
          "MapleMonoNormal-NF-CN"
          "MapleMonoNormal-NF-unhinted"
          "MapleMonoNormal-NF"
          "MapleMonoNormal-OTF"
          "MapleMonoNormal-TTF"
          "MapleMonoNormal-Variable"
          "MapleMonoNormal-Woff2"
          "MapleMonoNormalNL-CN-unhinted"
          "MapleMonoNormalNL-CN"
          "MapleMonoNormalNL-NF-CN-unhinted"
          "MapleMonoNormalNL-NF-CN"
          "MapleMonoNormalNL-NF-unhinted"
          "MapleMonoNormalNL-NF"
          "MapleMonoNormalNL-OTF"
          "MapleMonoNormalNL-TTF"
          "MapleMonoNormalNL-Variable"
          "MapleMonoNormalNL-Woff2")

for _var in "${_variant[@]}"; do
  _filename="${_var}.zip"
  noextract+=("${_filename}")
  source+=("${url}/releases/download/v${pkgver}/${_filename}")

  if [[ "${_var}" == *"OTF"* ]]; then
    _pkgname="otf-${_var//-OTF/}"
  elif [[ "${_var}" == *"Woff2"* ]]; then
    _pkgname="woff2-${_var//-Woff2/}"
  else
    _pkgname="ttf-${_var//-TTF/}"
  fi
  _pkgname=${_pkgname,,}
  pkgname+=("${_pkgname}")

  if [[ "${_var}" == "MapleMono-TTF" ]]; then
    _replaces="ttf-maple"
  elif [[ "${_var}" == "MapleMonoNL-TTF" ]]; then
    _replaces="ttf-maple-autohint"
  elif [[ "${_var}" == "MapleMono-OTF" ]]; then
    _replaces="otf-maple"
  elif [[ "${_var}" == "MapleMono-Woff2" ]]; then
    _replaces="woff2-maple"
  elif [[ "${_var}" == "MapleMono-NF" ]]; then
    _replaces="ttf-maple-nerd"
  elif [[ "${_var}" == "MapleMono-NF-CN" ]]; then
    _replaces="ttf-maple-sc-nerd"
  else
    _replaces=""
  fi

  _features=()
  if [[ "${_var}" == *"Normal"* ]]; then _features+=("使用normal预设"); fi
  if [[ "${_var}" == *"NL"* ]]; then _features+=("无连字"); fi
  if [[ "${_var}" == *"NF"* ]]; then _features+=("内嵌Nerd-Font"); fi
  if [[ "${_var}" == *"CN"* ]]; then _features+=("含中文"); fi
  if [[ "${_var}" == *"unhinted"* ]]; then _features+=("无微调"); fi
  if [[ "${_var}" == *"Variable"* ]]; then _features+=("可变"); fi

  if (( ${#_features[@]} > 0 )); then
    old_ifs="$IFS"
    IFS='，'
    _pkg_desc="${pkgdesc//。/}（${_features[*]}版本）"
    IFS=${old_ifs}
  fi

  _conflicts=""
  if [[ "${_pkgname}" == *"-unhinted" ]]; then
    _conflicts="${_pkgname//-unhinted/}"
  fi

  eval "package_${_pkgname}() {
  replaces=(${_replaces})
  conflicts=(${_conflicts})
  pkgdesc='${_pkg_desc}'

  _package '${_pkgname}' '${_filename}'
}"
done

_package() {
  local _pkg_name=$1
  local _filename=$2

  unzip "${_filename}" -d "${_pkg_name}"

  install -d "${pkgdir}/usr/share/fonts/maple"
  install -Dm644 -t "${pkgdir}/usr/share/fonts/maple" "${_pkg_name}"/Maple*

  install -d "${pkgdir}/usr/share/licenses/${_pkg_name}"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${_pkg_name}" "${_pkg_name}"/LICENSE.txt
}
