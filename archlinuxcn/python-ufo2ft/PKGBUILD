# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_name=ufo2ft
pkgname="python-${_name}"
pkgver=3.4.3
pkgrel=1
pkgdesc="A bridge from UFOs to FontTools objects"
arch=('any')
url="https://github.com/googlefonts/${_name}"
license=('MIT')
depends=(
    'python'
    'python-booleanoperations'
    'python-cffsubr'
    'python-compreffor'
    'python-defcon'
    'python-fontmath'
    'python-fonttools'
    'python-fs' # for fonttools[ufo]
    'python-lxml' # for fonttools[lxml]
    'python-skia-pathops'
    'python-typing_extensions'
    'python-ufolib2'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-setuptools-scm'
    'python-syrupy'
    'python-wheel'
)
checkdepends=(
    'python-pytest'
)
source=("${pkgname}::git+${url}.git#tag=v${pkgver}")
sha256sums=('c00a94bfcd788ed2c46f4cbf71e8f8156364e7bcc187fb5be6e1a0d7459e235d')

build() {
    cd "${srcdir}/${pkgname}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${pkgname}"
    python -m venv --system-site-packages .venv
    .venv/bin/python -m installer dist/*.whl
    .venv/bin/python -m pytest -v tests
}

package() {
    cd "${srcdir}/${pkgname}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
