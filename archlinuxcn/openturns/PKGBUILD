# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=openturns
pkgver=1.25.1
pkgrel=1
pkgdesc='Uncertainty treatment library'
license=('LGPL-3.0-or-later')
arch=('x86_64')
url='http://www.openturns.org'
depends=(
  bison
  blas
  boost
  eigen
  flex
  hdf5
  intel-tbb
  lapack
  libmpc
  libxml2
  muparser
  nlopt
  primesieve
  python-matplotlib
  python-pandas
  python-psutil
  python-scipy
  spectra
)
makedepends=(
  cmake
  swig
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/openturns/openturns/archive/v${pkgver}.tar.gz")
sha256sums=('10ab40ffa3240219bed0884ca7808655644adc25a8195a8cd1484318c267b9f7')

build() {
  cmake -B 'build' -S "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENTURNS_SYSCONFIG_PATH=/etc \
    -DCMAKE_SKIP_INSTALL_RPATH=ON
  make -C 'build'
}

package() {
  make DESTDIR="${pkgdir}" -C 'build' install
  python -O -m compileall "${pkgdir}/usr/lib/"
}
# vim:set ts=2 sw=2 et:

