# Maintainer: Scott Alfter <scott@alfter.us>
# Contributor: xiretza <xiretza+aur@xiretza.xyz>

_pkgname=amaranth
pkgname="python-$_pkgname"
pkgver=0.5.6
pkgrel=2
pkgdesc="A modern hardware definition language and toolchain based on Python (formerly nMigen)"
arch=(any)
url="https://github.com/amaranth-lang/amaranth"
license=('BSD-2-Clause')
depends=('python' 'python-jinja' 'python-pyvcd' 'python-jschon')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel'
             'python-setuptools-scm' 'python-packaging' 'python-pdm-backend')
optdepends=('python-paramiko: for remote builds')
provides=("python-nmigen=$pkgver" "python-$_pkgname=$pkgver")
conflicts=('python-nmigen' "python-nmigen-git" "python-$_pkgname-git")
replaces=('python-nmigen')
source=("git+https://github.com/amaranth-lang/amaranth.git#tag=v${pkgver}")
sha512sums=('3757837dfb6dea0304e8aadeede09fba0df29058b74ac04fa96d2fd66121602f6c65c49e227135cb2c83037b76bb489b25b3ecb81e288821b96c18d4a4d21b29')

prepare() {
  cd "${_pkgname}"

  sed -i -e "s/pdm\\.backend\\._vendor\\.//" pdm_build.py
  sed -i -e 's/"pdm-backend~=2.3.0",//' pyproject.toml
  sed -i -e 's/"pdm-backend~=2.3.0"//' pyproject.toml
}

build() {
  cd "${_pkgname}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
