# Maintainer: Integral <integral@member.fsf.org>
# Contributor: seth <getchoo at tuta dot io>

pkgname=joshuto
pkgver=0.9.9
pkgrel=1
pkgdesc="ranger-like terminal file manager written in Rust"
arch=('x86_64' 'aarch64')
url="https://github.com/kamiyaa/${pkgname}"
license=('LGPL-3.0-or-later')
makedepends=('cargo')
optdepends=(
	'fzf: for better file searching'
	'xclip: for clipboard support on X11'
	'xsel: for clipboard support on X11'
	'wl-clipboard: for clipboard support on Wayland'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('85a230183f7478dee7c29229d78313ee07b759e596e19292acf024d2e5735efa')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
