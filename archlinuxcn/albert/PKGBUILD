# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Evgenii Alekseev
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manuel Schneider  <manuelschneid3r at googles mail>

pkgname=albert
pkgver=0.30.1
pkgrel=2
pkgdesc="A sophisticated standalone keyboard launcher"
arch=('x86_64')
url="https://${pkgname}launcher.github.io"
license=('LicenseRef-Albert')
depends=(
	'libqalculate'
	'python'
	'python-pip'
	'qt6-base'
	'qt6-declarative'
	'qt6-5compat'
	'qt6-scxml'
	'qt6-shadertools'
	'qt6-svg'
	'qtkeychain-qt6'
)
makedepends=('cmake' 'qt6-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}launcher/${pkgname}/releases/download/v${pkgver}/v${pkgver}.tar.gz")
sha512sums=('a7340b89b5372365fc170583c62d2e844e18a47e76abc2a22163a98bc2173c70ec9f04c51b3ff705a671d26ec6525792b7a050c528f6d6b234284c5778c6635b')

build() {
	cmake -B build \
		-S "${pkgname}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_FIND_PACKAGE_RESOLVE_SYMLINKS=ON \
		-DQHOTKEY_INSTALL=OFF

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${pkgname}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
