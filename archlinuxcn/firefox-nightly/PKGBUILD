# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=firefox
_channel=nightly
pkgbase=firefox-nightly
pkgname=()
_version=141.0a1
pkgver=141.0a1.20250526.22
pkgrel=1
arch=('x86_64')
license=('MPL-2.0')
depends=('dbus-glib' 'gtk3' 'libxt' 'nss' 'mime-types')
optdepends=('pulseaudio: audio support'
            'ffmpeg: h.264 video'
            'hunspell: spell checking'
            'hyphen: hyphenation'
            'libnotify: notification integration'
            'networkmanager: location detection via available WiFi networks'
            'speech-dispatcher: text-to-speech'
            'xdg-desktop-portal: Screensharing with Wayland'
            'startup-notification: support for FreeDesktop Startup Notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-mozilla-central-l10n"
_filename_prefix="20250526.22-"
source=("${pkgbase}.desktop")
sha512sums=('b58c1b3b09772d0866d511e83e3c7f54db0fe71d2978d2a06bd19b25c020f16e201aab825f0b3d5e9f0c1a3e9db028018e5e38f7ee4868e321938f11cb5ac817'
            'd5d2d8378aba40afd76fc841e7f3b2a247808f61a0cf737c31d2077ac8e3b819b7fff0c9a2f27df3f87ee246ad44cd8d474161f6b6aa0c2b3dbe1a8179f6e329'
            'SKIP'
            'd5af96672f5f3f8566ad2e8ed98c0b90851060df983ffe2dd587502ed36a9ef057415d1b2909d55d7fd48b28ea613439c7f570d8e81a6b1f15d8b1881b2be67e'
            'SKIP'
            'e54c5b131b87ca8c61bf244bf6d95845814418b09d0b3b539697742168c26363fffcaaf62a7a552967460fa3adad3324784aa6a5adf2c949c7151674f9ebcf12'
            'SKIP'
            '82a75603818bf081bf5f0c0cf728b72b1b7a6e8522c5e2227eab055403422f2242ed9a51a819da7cd85504da3177a83af48409b0e599b9c376071aa1ce00a556'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353'
              '097B313077AE62A02F84DA4DF1A6668FBB7D572E') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=firefox-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.xz"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.xz"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.xz.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old firefox directory...'
    rm -rf firefox
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(firefox-${_channel}=$pkgver)
  conflicts=(firefox-${_channel} firefox-${_channel}-i18n-${1,,})
  replaces=(firefox-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}/browser
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/browser/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
pref("spellchecker.dictionary_path", "/usr/share/hunspell");
EOF

  # Disable auto-updates
  if [[ $1 == "zh-CN" ]]; then
    # Change homepage for zh_CN to avoid ADs and tracks
    cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true, "Homepage": {"URL": "about:home"}}}
EOF
  else
    cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF
  fi
}
