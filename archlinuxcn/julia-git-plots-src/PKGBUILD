pkgbase=julia-git-plots-src
_subpkgs=(RecipesBase
          RecipesPipeline)
pkgname=(julia-git-plots-src)
pkgver=1.40.18
_commit=986760b2d0a53cf50e0eaf70c57f4cf007cde7e7
pkgrel=1
pkgdesc="Plots.jl"
url="https://github.com/JuliaPlots/Plots.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaPlots/Plots.jl.git#commit=$_commit")
sha256sums=('71b165e3b9fa43b72d8219be4123a8dd7beb3bdc9d7a310d21fce462865081d7')

package_julia-git-plots-src() {
  cd Plots.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Plots "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/{RecipesBase,RecipesPipeline}
}

_subpackage() {
  local _subpkg=$1
  cd Plots.jl/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
