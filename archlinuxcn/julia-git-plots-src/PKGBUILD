pkgbase=julia-git-plots-src
_subpkgs=(RecipesBase
          RecipesPipeline)
pkgname=(julia-git-plots-src)
pkgver=1.40.13
_commit=d2b40c717258a859d3b80b8d30f7b9c40195873f
pkgrel=1
pkgdesc="Plots.jl"
url="https://github.com/JuliaPlots/Plots.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaPlots/Plots.jl.git#commit=$_commit")
sha256sums=('51f179b790ca3dc44612e2af034451b1b6e0a7e2cf49d7aa3020b3686e34a58f')

package_julia-git-plots-src() {
  cd Plots.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Plots "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/{RecipesBase,RecipesPipeline}
}

_subpackage() {
  local _subpkg=$1
  cd Plots.jl/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
