# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=uharfbuzz
pkgname=python-$_pyname
pkgver=0.50.0
pkgrel=1
pkgdesc='Streamlined Cython bindings for the HarfBuzz shaping engine'
arch=('x86_64')
url="https://github.com/harfbuzz/$_pyname"
license=('Apache-2.0')
depends=(
    'gcc-libs'
    'glibc'
    'python'
)
makedepends=(
    'cython'
    'python-build'
    'python-installer'
    'python-pkgconfig'
    'python-setuptools'
    'python-setuptools-scm'
    'python-wheel'
)
checkdepends=('python-pytest')
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('36380fd047452cc72f02b864d270f953a3c6aced4dc5748464b2adec5e609753')

build() {
    cd "$srcdir/$_archive"
    python -m build --wheel --no-isolation
}

check() {
    cd "$srcdir/$_archive"
    python -m venv --system-site-packages .venv
    .venv/bin/python -m installer dist/*.whl
    .venv/bin/python -m pytest -v -r a
}

package() {
    cd "$srcdir/$_archive"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
