# Maintainer: Hu Butui <hot123tea123@gmail.com>

pkgname=spectra
pkgver=1.2.0
pkgrel=1
pkgdesc="A header-only C++ library for large scale eigenvalue problems"
license=('MPL-2.0')
arch=('any')
url='https://spectralib.org'
makedepends=(
  clang
  cmake
  eigen
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/yixuan/spectra/archive/v${pkgver}.tar.gz")
sha512sums=('dfd8824d33853dc23c476af4ca8458393284e1d50b8524c000b14109df0095257c3975881d01286bdd057950366d0ff498afb63fbde02f9f313122212ddf62a6')

build() {
  cmake \
    -B "build" \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTS=ON \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_DATADIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S "${srcdir}/${pkgname}-${pkgver}"
  make -C build
}

check() {
  cd build
  ctest --verbose
}

package() {
  make -C build install DESTDIR="${pkgdir}"
}
# vim:set ts=2 sw=2 et:
