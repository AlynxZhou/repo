pkgname=julia-git-enzyme_jll-src
pkgver=0.0.181+0
_commit=ccf3213d7a64eb148be16b5d893eeb8991b1cb68
pkgrel=1
pkgdesc="Enzyme_jll.jl"
url="https://github.com/JuliaBinaryWrappers/Enzyme_jll.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts llvm)
depends=(julia-git enzyme-julia)
source=("git+https://github.com/JuliaBinaryWrappers/Enzyme_jll.jl.git#commit=$_commit"
        jll.toml.in)
sha256sums=('9aa6cfa6bd921fb52b9d9f19c83eff0941a24c07cfc1f90504a656d8b3037fb7'
            '7fe88d60082a7d6de7f775f36bef4247c04be3c9bd3f3c923851ebcf5ef86a84')

build() {
  _llvm_ver=$(llvm-config --version)
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}

  sed -e "s/@LLVM_VER@/${llvm_maj_ver}/g" jll.toml.in > jll.toml

  cd Enzyme_jll.jl

  julia /usr/lib/julia/julia-gen-jll.jl Enzyme_jll ../jll.toml
}

package() {
  cd Enzyme_jll.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Enzyme_jll "${pkgdir}" "${pkgname}" julia-git
}
