# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-lily
pkgver=570.144
pkgrel=1
pkgdesc="NVIDIA kernel modules for linux-lily"
arch=('x86_64')
url="https://www.nvidia.com/"
makedepends=('linux-lily' 'linux-lily-headers' "nvidia-dkms=$pkgver")
provides=('NVIDIA-MODULE')
license=('custom')
options=('!strip')
conflicts=('nvidia-dkms')

build() {
    _kernver=$(</usr/src/linux-lily/version)

    fakeroot dkms build --dkmstree "${srcdir}" -m nvidia/${pkgver} -k ${_kernver}
}

package() {
    depends=("linux-lily=6.14.2-2" "nvidia-utils=${pkgver}" 'libglvnd')

    _kernver=$(</usr/src/linux-lily/version)

    install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 nvidia/${pkgver}/${_kernver}/${CARCH}/module/*.ko

    # compress each module individually
    find "$pkgdir" -name '*.ko' -exec zstd --rm -19 {} +

    install -Dm644 /usr/share/licenses/nvidia-dkms/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
