# Maintainer: Hu Butui <hot123tea123@gmail.com>

pkgname=sentencepiece
pkgver=0.2.1
pkgrel=2
pkgdesc="Unsupervised text tokenizer for Neural Network-based text generation"
arch=('x86_64')
url="https://github.com/google/sentencepiece"
license=('Apache-2.0')
depends=(
  abseil-cpp
  gcc-libs
  gperftools
  glibc
  protobuf
)
makedepends=(
  cmake
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/google/sentencepiece/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('012850b63b2323e16acc5dacc0a494ad3f6375425ee86274f0946032e47c088a3b307758b99d752fcf54acf76c82d7d13d0c14bbf07aa9b612c4f1fbd30cf1cf')

build() {
  # workaround gcc15
  export CXXFLAGS="-include cstdint"
  # workaround cmake 4
  cmake \
    -B "${srcdir}/build" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSPM_BUILD_TEST=OFF \
    -DSPM_ABSL_PROVIDER=package \
    -DSPM_ENABLE_TCMALLOC=ON \
    -DSPM_ENABLE_SHARED=ON \
    -DSPM_PROTOBUF_PROVIDER=package \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -S "${srcdir}/${pkgname}-${pkgver}"
  make -C "${srcdir}/build"

}

package() {
  provides=(
    libsentencepiece.so
    libsentencepiece_train.so
  )
  DESTDIR="${pkgdir}" make -C "${srcdir}/build" install
}
# vim:set ts=2 sw=2 et:


