# Maintainer: Integral <integral@member.fsf.org>
# Contributor: farwayer <farwayer@gmail.com>
# Co-maintainer: Markus Hartung (harre) <mail@hartmark.se>
# Contributer: Danct12 <danct12@disroot.org>
# Contributor: Bart Ribbers <bribbers@disroot.org>

pkgname=waydroid-image-gapps
pkgver=18.1_20250628
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="LineageOS-based Android images for Waydroid (with GApps included)"
arch=('x86_64')
license=('Apache-2.0')
url="https://waydro.id"
depends=('waydroid')
provides=('waydroid-image')
conflicts=('waydroid-image')
_srcprefix="https://sourceforge.net/projects/waydroid/files/images"
source=("${_srcprefix}/system/lineage/waydroid_x86_64/lineage-${_pkgver}-GAPPS-waydroid_x86_64-system.zip"
	"${_srcprefix}/vendor/waydroid_x86_64/lineage-${_pkgver}-MAINLINE-waydroid_x86_64-vendor.zip")
sha256sums=('8e2b1546db9b194939d12ef1a6d5d9d0c76353388ec86f3c260bb94ed760b4c2'
            '5414a273ac972aa1a65bcca597d5d32933d846dd1628cfdc92413b7da5332189')

package() {
	install -Dm644 {system,vendor}.img -t "${pkgdir}/usr/share/waydroid-extra/images/"
}
