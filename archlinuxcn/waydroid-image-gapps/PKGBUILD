# Maintainer: Integral <integral@member.fsf.org>
# Contributor: farwayer <farwayer@gmail.com>
# Co-maintainer: Markus Hartung (harre) <mail@hartmark.se>
# Contributer: Danct12 <danct12@disroot.org>
# Contributor: Bart Ribbers <bribbers@disroot.org>

pkgname=waydroid-image-gapps
pkgver=18.1_20250510
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="LineageOS-based Android images for Waydroid (with GApps included)"
arch=('x86_64')
license=('Apache-2.0')
url="https://waydro.id"
depends=('waydroid')
provides=('waydroid-image')
conflicts=('waydroid-image')
_srcprefix="https://sourceforge.net/projects/waydroid/files/images"
source=("${_srcprefix}/system/lineage/waydroid_x86_64/lineage-${_pkgver}-GAPPS-waydroid_x86_64-system.zip"
	"${_srcprefix}/vendor/waydroid_x86_64/lineage-${_pkgver}-MAINLINE-waydroid_x86_64-vendor.zip")
sha256sums=('71c98db2a239aa6c376d0ba9a19adba35640ca99b0f4b6dd200b4da41f06d499'
            'df62fb6b5dd7ffe2048bb8e265eb5d007a025f2e57f5c486a489a18952e37248')

package() {
	install -Dm644 {system,vendor}.img -t "${pkgdir}/usr/share/waydroid-extra/images/"
}
