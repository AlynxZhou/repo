# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=speakersafetyd
pkgver=1.1.2
pkgrel=1
pkgdesc='Asahi Linux speaker safety daemon'
arch=('aarch64')
url='http://asahilinux.org'
depends=('alsa-lib')
makedepends=('rust')
options=('!emptydirs')
license=('MIT')
source=(
  "speakersafetyd-${pkgver}.tar.gz::https://github.com/AsahiLinux/speakersafetyd/archive/refs/tags/${pkgver}.tar.gz"
  speakersafetyd.sysusers
)
sha256sums=('b3ccbbf4c3ee0da537203186f80eb8f3cc16037bf41f4cd0de50b7cd25dd713f'
            '3eedb6ddce80f888e0c47016c3773776c91942bcd7a08da8740daddf403c6bbb')

build() {
  cd "speakersafetyd-${pkgver}"
  make
}

package() {
  cd "speakersafetyd-${pkgver}"
  make install \
       DESTDIR="${pkgdir}" \
       UNITDIR=/usr/lib/systemd/system \
       UDEVDIR=/usr/lib/udev/rules.d \
       SPEAKERSAFETYD_USER=root \
       SPEAKERSAFETYD_GROUP=root
  install -Dm644 "${srcdir}/speakersafetyd.sysusers" \
          "${pkgdir}/usr/lib/sysusers.d/speakersafetyd.conf"
}
