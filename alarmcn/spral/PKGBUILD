pkgname=spral
pkgver=2025.05.20
pkgrel=2
pkgdesc="Sparse Parallel Robust Algorithms Library"
arch=('aarch64' 'x86_64')
url="https://github.com/ralna/spral"
license=('BSD')
makedepends=(gcc-fortran meson)
depends=('metis' 'lapack' 'hwloc')
source=("https://github.com/ralna/spral/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('77c03b52b5f52b5dff7fb5338be7518e09b1bdf0c0c8899c8938377b9ff240da')

build() {
  cd $pkgname-$pkgver
  arch-meson build -Dtests=false -Dmodules=false
  meson compile -C build
}

package () {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
