# Maintainer: c0gnitivefl0w
# Co-Maintainer: eldios

pkgbase=seadrive-daemon
pkgname=(seadrive-daemon python-seadrive)
pkgver=3.0.16
pkgrel=2
pkgdesc="SeaDrive daemon with FUSE interface"
arch=('aarch64' 'armv7h' 'x86_64')
url="https://github.com/haiwen/seadrive-fuse"
license=('GPL-3.0-only')
_daemon_depends=(libcrypto.so libuuid.so libsqlite3.so libevent libz.so libcurl.so
                 fuse2 libsearpc libgobject-2.0.so libglib-2.0.so
                 jansson libwebsockets.so libargon2.so)
makedepends=(git intltool python "${_daemon_depends[@]}")
source=("git+https://github.com/haiwen/seadrive-fuse#tag=v${pkgver}"
        0001-Fix-argment-type.patch)
sha256sums=('033ff085c27fcca07bfc4dc55330b358d2a02a761f907ca76969921cdcc6c396'
            '166c4a463fb4360e73d3603b066eecea852cc7201ca6fee760f384468d4fb514')

prepare() {
  cd seadrive-fuse

  patch -Np1 --no-backup-if-mismatch < "../0001-Fix-argment-type.patch"
}

build() {
  cd seadrive-fuse

  ./autogen.sh
  ./configure --prefix=/usr
  make

  make install DESTDIR="${PWD}/pkg"
}

package_seadrive-daemon() {
  depends=("${_daemon_depends[@]}")
  cd seadrive-fuse

  mkdir -p "${pkgdir}/usr/bin/"
  cp -a pkg/usr/bin/seadrive "${pkgdir}/usr/bin/seadrive"
}

package_python-seadrive() {
  depends=(python libsearpc)
  cd seadrive-fuse

  mkdir -p "${pkgdir}/usr/lib/"
  cp -a pkg/usr/lib/python* "${pkgdir}/usr/lib"
}
